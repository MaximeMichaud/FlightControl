plugins {
    id 'io.freefair.lombok' version '4.1.6'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

configurations {
    // configuration that holds jars to include in the jar
    includeInJar
    implementation.extendsFrom(includeInJar)
}

dependencies {
    includeInJar 'org.bstats:bstats-bukkit-lite:1.5'
    includeInJar project(':API')
    includeInJar project(':Multiversion')
    includeInJar project(':VersionOld')
    includeInJar project(':VersionCurrent')

    compileOnly 'org.jetbrains:annotations:13.0'
    compileOnly('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
    }
    compileOnly 'me.badbones69:crazyenchantments-plugin:1.8-Dev-Build-v7.1'
    compileOnly 'net.ess3:EssentialsX:2.17.1'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:6.1.3-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-legacy:6.1.3-SNAPSHOT'
    compileOnly 'com.plotsquared:plotsquared-api:19.12.10-b4c257b-788'
    compileOnly 'com.github.IntellectualSites.PlotSquared:Core:4.390'
    compileOnly 'com.github.Angeschossen:LandsAPI:4.3.3.7'

    compileOnly files('../lib/CombatLogX-9.9.2.1.jar')
    compileOnly files('../lib/DeluxeCombat_v.1.14.2.jar')
    compileOnly files('../lib/DummyCombatTagPlus.jar')
    compileOnly files('../lib/CombatLogPro-v1.5.9.jar')
    compileOnly files('../lib/AntiCombatLoggingAPI.jar')
    compileOnly files('../lib/Towny-0.93.0.0.jar')
}

processResources {
    eachFile { details ->
        if (details.name.endsWith('.yml')) { // or whatever pattern/criteria is appropriate
            filter {
                String line -> line.replaceAll('\\$\\{revision}', version.toString())
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'org.bstats.bukkit', 'org.spazzinq.flightcontrol.metric'
    archiveName 'FlightControl.jar'
}